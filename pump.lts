
//
// 17-651: FSP model of a simple infusion pump
//
// ASSUMPTIONS
// - The user can choose, in the setup phase, which item of info to enter next.
// - The simple enter_value action is a simple number pad where the user enters
//   numeric values.
// - There exist 'set' and 'cancel' buttons, which are the targets of press_set
//   and press_cancel.
// - When the user turns the unit off, the unit will be in a state as if it was
//   just plugged in.
//

//
// Set of actions that the user of the LTSA tool can control in an
// animation of this model.
//
menu UserControlMenu = {
    change_settings, clear_rate, confirm_settings, connect_set, set_time,
    dispense_main_med_flow, enter_value, erase_and_unlock_line,
    flow_unblocked, sound_alarm, lock_unit, press_cancel, time_off,
    press_set, set_rate, silence_alarm, turn_off, unlock_unit, primary_power_off,
    flow_blocked, setup_mode, pressure_is_ok
}

//======================
// Constants and Ranges
//======================

//
// States of the pump alarm
//
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated .. AlarmSilenced

//
// States of the pump settings
//
const ParamsNotSet = 2    // pump parameters not set yet
const ParamsSet    = 3    // pump parameters already set

range ParamsStateT = ParamsNotSet .. ParamsSet

//
// Locked/unlocked states of a line with respect to a pump channel
//
const LineUnlocked = 4  // line not locked into a pump channel 
const LineLocked   = 5  // line locked into a pump channel

range LineLockStateT = LineUnlocked .. LineLocked

//
// Locked/unlocked states of the pump unit
//
const UnitUnlocked = 6  // the keypad of the pump is not locked
const UnitLocked   = 7  // the keypad of the pump is locked

range UnitLockStateT = UnitUnlocked .. UnitLocked

const SystemCheckRequired      = 8  // 
const SystemCheckNotRequired   = 9  //

range SystemCheckStateT = SystemCheckRequired .. SystemCheckNotRequired

//
// Pressure is fine or not
//
const PressureIsNotOk = 10  // pressure in the pump is not ok
const PressureIsOk   = 11  // pressure in the pump is ok

range PressureStateT = PressureIsNotOk .. PressureIsOk

//=====================
// Process Definitions
//=====================

PUMP = POWER_OFF,

// Pump goes to battery mode if the primary power is off - unplugged or 
// electricity in the circuit unexpectedly disappears.
BATTERY_MODE[alarm:AlarmStateT] = (
    turn_off -> POWER_OFF
	|
    when (alarm == AlarmActivated)
        sound_alarm -> BATTERY_MODE[alarm]
    |
    when (alarm == AlarmActivated)
        silence_alarm -> BATTERY_MODE[AlarmSilenced]
),

POWER_OFF = 
(
    turn_on -> SYSTEM_CHECK_ON_INITIALIZATION
),

//
// Performing system checks when the pump is plugged in.
// In case of any errors display them.
//
SYSTEM_CHECK_ON_INITIALIZATION = (
	system_check_success -> SETUP_MODE
	|
	system_check_fail -> DISPLAY_ERROR
),

DISPLAY_ERROR = (
	display_error -> DISPLAY_ERROR
	|
	turn_off -> POWER_OFF
),

// In order to start setting parameters a user should make some explicit action, 
// for example, pushing a certain button.
SETUP_MODE = (
    primary_power_off -> BATTERY_MODE[AlarmActivated]
    |
    turn_off -> POWER_OFF
    |
    setup_mode -> SETUP[ParamsNotSet][LineUnlocked][PressureIsNotOk]
    |
    setup_mode -> SETUP[ParamsNotSet][LineUnlocked][PressureIsOk]
),

//
// Pump in setup mode:
// - Once required pump parameters (just rate in this case) are set,
//   physical connections can be made and line can be locked
//
SETUP[params:ParamsStateT][lineLock:LineLockStateT][pressure:PressureStateT] = 
(
    primary_power_off -> BATTERY_MODE[AlarmActivated]
    |
    turn_off -> POWER_OFF
    |
    when (params == ParamsNotSet && lineLock == LineUnlocked)
        set_rate -> enter_value ->
            (press_set -> SETUP[ParamsSet][lineLock][pressure]
             |
             press_cancel -> SETUP[ParamsNotSet][lineLock][pressure])
    |
    when (params == ParamsSet && lineLock == LineUnlocked)
        clear_rate -> SETUP[ParamsNotSet][lineLock][pressure]
    |
    when (params == ParamsSet && lineLock == LineUnlocked)
        connect_set -> set_time-> purge_air -> lock_line -> SETUP[params][LineLocked][pressure]
    |
    when (lineLock == LineLocked && pressure == PressureIsNotOk)
        confirm_settings -> PRESSURE_CHECK[AlarmActivated]
    |
    when (lineLock == LineLocked && pressure == PressureIsOk)
        confirm_settings -> PRESSURE_CHECK[AlarmSilenced]
    |
    when (lineLock == LineLocked)
        erase_and_unlock_line -> SETUP[params][LineUnlocked][pressure]
),

//
// Pump in infusion mode:
// - Always be able to turn the unit off, even if locked
// - Allow the user to lock/unlock the unit
// - Errors could occur with the pump (e.g., line became pinched or plugged)
// - Do system check if it is required
//
INFUSION[unitLock:UnitLockStateT][systemCheck:SystemCheckStateT] =
(
	primary_power_off -> BATTERY_MODE[AlarmActivated]
	|
    time_off -> POWER_OFF
    |
	when (systemCheck == SystemCheckRequired)
		do_system_check -> PERIODIC_SYSTEM_CHECK[unitLock]
	|
    when (systemCheck == SystemCheckNotRequired)
		turn_off -> POWER_OFF
    |
	when (unitLock == UnitUnlocked & systemCheck == SystemCheckNotRequired)
    	change_settings -> SETUP[ParamsSet][LineLocked][PressureIsOk]
    |
    when (unitLock == UnitUnlocked & systemCheck == SystemCheckNotRequired)
      	lock_unit -> INFUSION[UnitLocked][SystemCheckRequired]
    |
    when (unitLock == UnitLocked & systemCheck == SystemCheckNotRequired)
       	unlock_unit -> INFUSION[UnitUnlocked][SystemCheckRequired]
    |
	when (systemCheck == SystemCheckNotRequired)
    	dispense_main_med_flow -> INFUSION[unitLock][SystemCheckRequired]
    |
	when (systemCheck == SystemCheckNotRequired)
    	flow_blocked -> sound_alarm -> INFUSION_BLOCKED[unitLock][AlarmActivated]
	
),

//
// Periodic system checks
//
PERIODIC_SYSTEM_CHECK[unitLock:UnitLockStateT] =
(
	system_check_success -> INFUSION[unitLock][SystemCheckNotRequired]
	|
	system_check_fail -> DISPLAY_ERROR
),

//
// Pump stuck (medication flow suspended)
//
INFUSION_BLOCKED[unitLock:UnitLockStateT][alarm:AlarmStateT] =
(
    primary_power_off -> BATTERY_MODE[AlarmActivated]
    |
    turn_off -> POWER_OFF
    |
    when (alarm == AlarmActivated)
        sound_alarm -> INFUSION_BLOCKED[unitLock][alarm]
    |
    when (alarm == AlarmActivated)
        silence_alarm -> INFUSION_BLOCKED[unitLock][AlarmSilenced]
    |
    flow_unblocked -> INFUSION[unitLock][SystemCheckRequired]
),

//
// Pressure checking
//
PRESSURE_CHECK[alarm:AlarmStateT] =
(
    primary_power_off -> BATTERY_MODE[AlarmActivated]
    |
    turn_off -> POWER_OFF
    |
    when (alarm == AlarmActivated)
        sound_alarm -> PRESSURE_CHECK[alarm]
    |
    when (alarm == AlarmActivated)
        silence_alarm -> PRESSURE_CHECK[AlarmSilenced]
    |
    pressure_is_ok -> INFUSION[UnitUnlocked][SystemCheckRequired]
).
